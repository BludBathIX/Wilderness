#!/usr/bin/perl

use strict;
use warnings;

use World;
use Player;
use Data::Dumper;

my $world = World->_new();
my $player = Player->_new(location => $world->{'grid'}->{'0,0,0'});
$world->{'grid'}->{'0,0,0'}->_add_item($player);

my %alternates = (
    have    => 'inventory',
    grab    => 'take',
    get     => 'take',
    pickup  => 'take',
    retrieve => 'take',
    walk    => 'go',
    travel  => 'go',
    move    => 'go',
    exit    => 'go',
    attack  => 'kill',
    speak   => 'say',
    talk    => 'say',
    create  => 'make',
    craft   => 'make',
    build   => 'make',
    place   => 'put',
);

print "\n\nWelcome to the World of Awesome.\n";
print "Do you have what it takes to survive?\n\n";

# The introductory statement
$player->look();
print "\n";
$player->inventory();

while ( my ($verb, @arguments) = _prompt() ) {
    if ( $verb =~ /^\_/ ) {
        warn "\tFor your own safety, I would recommend against using '_'\n"
            . "\tThe last time a player used that, they were stepped on by Big Foot\n"
            if $verb eq '_';
        warn "\tYou have to actually TYPE what you want to do.\n"
            . "\tMy crystal ball broke, so I gave up on the mind-reading thing\n"
            if $verb eq '_empty_';
        next;
    }
    if ( my $m = $player->can($alternates{$verb} || $verb) ) {
        my @args = $player->_str2obj(@arguments);
        $player->$m(@args);
    }
    else {
        warn "\tYou do not know how to $verb\n";
    }
}

# Modify prompt to not return until it gets an input
sub _prompt {
    print "\nWhat would you like to do? ";
    my $command = <STDIN>;
    chomp $command;
    return "_" if $command =~ /\_/;
    return "_empty_" if $command =~ /^\s*$/;
    $command = lc $command;
    $command =~ s/\bthe\b//;
    $command =~ s/\ba\b//;
    $command =~ s/\bat\b//;
    return split /\s+/, $command;
}
